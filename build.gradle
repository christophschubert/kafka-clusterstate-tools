plugins {
    id 'java'
}

group 'net.christophschubert'
version '1.0.1-SNAPSHOT'

sourceCompatibility = 1.11

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    group 'net.christophschubert'
    version '1.0.1-SNAPSHOT'
}

jar {
    manifest {
        attributes "Main-Class": "net.christophschubert.kafka.clusterstate.cli.CLI"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://packages.confluent.io/maven'
    }
    maven { //necessary for JSON schema dependency
        url 'https://jitpack.io'
    }


}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

task integrationTest(type: Test) {
    description = "Runs integration tests"
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

dependencies {

    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.9'

    compile group: 'info.picocli', name: 'picocli', version: '4.5.1'

    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

    compile group: 'io.confluent', name: 'kafka-avro-serializer', version: '6.0.0'
    compile group: 'io.confluent', name: 'kafka-protobuf-serializer', version: '6.0.0'
    compile group: 'io.confluent', name: 'kafka-json-schema-serializer', version: '6.0.0'

    testCompile group: 'org.apache.kafka', name: 'kafka-streams', version: '2.6.0', classifier: 'test'




    compile group: 'org.apache.kafka', name: 'kafka-clients', version: '2.6.0'
    testCompile group: 'org.apache.kafka', name: 'kafka-clients', version: '2.6.0'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.10.3'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.10.3'

    intTestImplementation "org.testcontainers:testcontainers:1.15.0"
    intTestImplementation "org.testcontainers:kafka:1.15.0"
    intTestImplementation group: 'junit', name: 'junit', version: '4.12'

}